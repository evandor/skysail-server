// Available to customize the build

plugins {
    id "org.asciidoctor.convert" version "1.5.3"
    id 'com.github.jruby-gradle.base' version '1.4.0'
}

apply plugin: 'eclipse'
apply plugin: 'java'

dependencies {
    gems files('gradle/gems/asciidoctor-diagram-1.5.4.gem')
}

asciidoctorj {
    version = '1.5.5'
}

task copyImagesFromBundles(type: Copy) {
    from 'skysail.domain/src/docs/asciidoc/images'
    into 'build/asciidoc/html5/images'

    from 'skysail.server.doc/src/docs/asciidoc/images'
    into 'build/asciidoc/html5/images'
}

asciidoctor {
    dependsOn copyImagesFromBundles, jrubyPrepare
    requires = ['asciidoctor-diagram']
    gemPath = jrubyPrepare.outputDir
    attributes  \
         'build-gradle': file('build.gradle'),
            'source-highlighter': 'coderay',
            'imagesdir': 'images',
            'toc': 'left',
            'icons': 'font',
            'setanchors': '',
            'idprefix': '',
            'idseparator': '-',
            'docinfo1': ''
}

task wrapper(type: Wrapper) {
    jarFile = rootProject.file('.gradle-wrapper/gradle-wrapper.jar')
}

task createAndCopyDocumentationToDocBundle(dependsOn: asciidoctor, type: Copy) {
    from 'build/asciidoc/html5'
    into 'skysail.server.doc/resources/assets/html5'
}

// https://stackoverflow.com/questions/13879667/how-to-fix-test-reports-were-found-but-none-of-them-are-new-did-tests-run-in
task jenkinsTest{
    inputs.files test.outputs.files
    doLast{
        def timestamp = System.currentTimeMillis()
        test.getReports().getJunitXml().getDestination().eachFile { it.lastModified = timestamp }
    }
}

build.dependsOn(jenkinsTest)

configure(createAndCopyDocumentationToDocBundle) {
    group = 'documentation'
    description = 'Creates Asciidoc html5 documentation and copies it to skysail.server.doc'
}
